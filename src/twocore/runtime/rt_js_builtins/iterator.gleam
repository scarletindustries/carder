//// `rt_js_builtins/iterator` — %IteratorPrototype% / %AsyncIteratorPrototype%
//// + the per-kind iterator prototypes + %AsyncFromSyncIteratorPrototype%
//// (SPEC §7.M6 builtin-control §27.1). Port of arc `builtins.gleam:99-220`
//// iterator-proto bootstrap + arc `exec/promises.gleam:1791-2003`
//// Async-from-Sync dispatch, re-expressed over threaded `InstanceState` (R1).

import gleam/dict
import gleam/list
import gleam/option.{type Option, None, Some}
import twocore/runtime/rt_js_async
import twocore/runtime/rt_js_builtins/common
import twocore/runtime/rt_js_builtins/helpers.{arg_at, first_arg_or_undefined}
import twocore/runtime/rt_js_builtins/iter_protocol.{
  IterateStrings, RejectPrimitives,
}
import twocore/runtime/rt_js_builtins/js_string
import twocore/runtime/rt_js_builtins/limits
import twocore/runtime/rt_js_builtins/promise as b_promise
import twocore/runtime/rt_js_builtins/realm_ops
import twocore/runtime/rt_js_call
import twocore/runtime/rt_js_obj
import twocore/runtime/rt_js_ops
import twocore/runtime/rt_js_ordered_entries as ordered_entries
import twocore/runtime/rt_js_store
import twocore/runtime/rt_js_types.{
  type ArrayIterKind, type BuiltinPair, type ConcatItem, type GeneratorState,
  type Handle, type HelperBody, type IteratorHelperKind, type IteratorNative,
  type IteratorRecord, type JsVal, type MapIterKind, type NativeToken,
  type ObjKind, type ObjectKey, type SetIterKind, type ZipMember, type ZipMode,
  ArgumentsObj, ArrayBufferObj, ArrayIterEntries, ArrayIterKeys, ArrayIterValues,
  ArrayIterator, ArrayObj, AsyncFromSyncClose, AsyncFromSyncIterator, AsyncFromSyncNext,
  AsyncFromSyncReturn, AsyncFromSyncThrow, AsyncFromSyncUnwrap, ClassicHelper,
  ConcatHelper, ConcatItem, GenCompleted, GenExecuting, GenSuspendedStart,
  GenSuspendedYield, HelperDrop, HelperFilter, HelperFlatMap, HelperMap,
  HelperTake, IteratorConstructor, IteratorHelperObj, IteratorN, JFloat, JInt,
  JNan, JNegInf, JPosInf, KHandle, KNull, KStr, KUndef,
  MapIterEntries, MapIterKeys, MapIterValues, MapIterator, MapObj, Named,
  NoElements, Ordinary, RangeErr, ReturnThis, SObject, SetIterEntries,
  SetIterValues, SetIterator, SetObj, StringIterator, StringKey, SymbolKey,
  TypeErr, TypedArrayObj, WrapForValidIteratorObj, ZipExhausted, ZipHelper,
  ZipLongest, ZipOpen, ZipShortest, ZipStrict, classify, index_key,
  map_key_to_js, mk_bool, mk_number, mk_object, mk_string, mk_undefined,
  symbol_async_iterator, symbol_iterator, symbol_to_string_tag,
}
import twocore/runtime/rt_js_val
import twocore/runtime/rt_state.{type InstanceState}

/// The iterator-prototype set `init_realm` needs.
pub type IteratorProtos {
  IteratorProtos(
    iterator_proto: Handle,
    array_iter_proto: Handle,
    string_iter_proto: Handle,
    map_iter_proto: Handle,
    set_iter_proto: Handle,
    async_iterator_proto: Handle,
    async_from_sync_proto: Handle,
    iterator: BuiltinPair,
    iterator_helper_proto: Handle,
    wrap_for_valid_proto: Handle,
  )
}

/// Allocate %IteratorPrototype% (`[@@iterator]() { return this }`) plus the
/// four kind-specific iterator prototypes and %AsyncIteratorPrototype%. Port
/// of arc `builtins.gleam:99-199`.
pub fn init(
  st: InstanceState,
  object_proto: Handle,
  fn_proto: Handle,
) -> #(IteratorProtos, InstanceState) {
  // %IteratorPrototype% — [@@iterator]() { return this }.
  let #(iter_sym_fn, st) =
    common.alloc_rooted_native_fn(
      st,
      fn_proto,
      ReturnThis,
      "[Symbol.iterator]",
      0,
    )
  let #(iterator_proto, st) =
    alloc_proto_with_symbol(st, object_proto, symbol_iterator, iter_sym_fn)
  // Per-kind iterator prototypes: proto → %IteratorPrototype%.
  let #(array_iter_proto, st) =
    alloc_iter_proto(
      st,
      fn_proto,
      iterator_proto,
      IteratorN(rt_js_types.ArrayIteratorNext),
      "Array Iterator",
    )
  let #(string_iter_proto, st) =
    alloc_iter_proto(
      st,
      fn_proto,
      iterator_proto,
      IteratorN(rt_js_types.StringIteratorNext),
      "String Iterator",
    )
  let #(map_iter_proto, st) =
    alloc_iter_proto(
      st,
      fn_proto,
      iterator_proto,
      IteratorN(rt_js_types.MapIteratorNext),
      "Map Iterator",
    )
  let #(set_iter_proto, st) =
    alloc_iter_proto(
      st,
      fn_proto,
      iterator_proto,
      IteratorN(rt_js_types.SetIteratorNext),
      "Set Iterator",
    )
  // %AsyncIteratorPrototype% — [@@asyncIterator]() { return this }.
  let #(async_sym_fn, st) =
    common.alloc_rooted_native_fn(
      st,
      fn_proto,
      ReturnThis,
      "[Symbol.asyncIterator]",
      0,
    )
  let #(async_iterator_proto, st) =
    alloc_proto_with_symbol(
      st,
      object_proto,
      symbol_async_iterator,
      async_sym_fn,
    )
  // %AsyncFromSyncIteratorPrototype% — §27.1.4.2 next/return/throw.
  let #(afs_methods, st) =
    common.alloc_methods(st, fn_proto, [
      #("next", IteratorN(AsyncFromSyncNext), 1),
      #("return", IteratorN(AsyncFromSyncReturn), 1),
      #("throw", IteratorN(AsyncFromSyncThrow), 1),
    ])
  let #(async_from_sync_proto, st) =
    common.alloc_proto(
      st,
      Some(async_iterator_proto),
      common.named_props(afs_methods),
    )
  // ── ES2025 §27.1 Iterator constructor + prototype helpers ─────────────────
  // Iterator.prototype methods — eager consumers + lazy producers.
  let #(proto_methods, st) =
    common.alloc_methods(st, fn_proto, [
      #("map", IteratorN(rt_js_types.IteratorPrototypeMap), 1),
      #("filter", IteratorN(rt_js_types.IteratorPrototypeFilter), 1),
      #("take", IteratorN(rt_js_types.IteratorPrototypeTake), 1),
      #("drop", IteratorN(rt_js_types.IteratorPrototypeDrop), 1),
      #("flatMap", IteratorN(rt_js_types.IteratorPrototypeFlatMap), 1),
      #("toArray", IteratorN(rt_js_types.IteratorPrototypeToArray), 0),
      #("forEach", IteratorN(rt_js_types.IteratorPrototypeForEach), 1),
      #("reduce", IteratorN(rt_js_types.IteratorPrototypeReduce), 1),
      #("some", IteratorN(rt_js_types.IteratorPrototypeSome), 1),
      #("every", IteratorN(rt_js_types.IteratorPrototypeEvery), 1),
      #("find", IteratorN(rt_js_types.IteratorPrototypeFind), 1),
    ])
  // Iterator.from / concat / zip / zipKeyed static methods.
  let #(ctor_props, st) =
    common.alloc_methods(st, fn_proto, [
      #("from", IteratorN(rt_js_types.IteratorFrom), 1),
      #("concat", IteratorN(rt_js_types.IteratorConcat), 0),
      #("zip", IteratorN(rt_js_types.IteratorZip), 1),
      #("zipKeyed", IteratorN(rt_js_types.IteratorZipKeyed), 1),
    ])
  // Constructor + merge proto methods onto the existing %IteratorPrototype%.
  // §27.1.3.1: Iterator is an abstract constructor — HAS [[Construct]], but
  // `new Iterator()` directly throws (enforced in `dispatch_construct`).
  let #(iterator, st) =
    common.init_type_on(
      st,
      iterator_proto,
      fn_proto,
      proto_methods,
      fn(_proto) { IteratorN(IteratorConstructor) },
      "Iterator",
      0,
      ctor_props,
      True,
    )
  // §27.1.3.2/.13: constructor + [@@toStringTag] are ACCESSOR properties
  // (SetterThatIgnoresPrototypeProperties). init_type_on wrote a data
  // .constructor — overwrite with the accessor.
  let #(ctor_acc, st) =
    common.alloc_get_set_accessor(
      st,
      fn_proto,
      IteratorN(rt_js_types.IteratorProtoGetConstructor),
      IteratorN(rt_js_types.IteratorProtoSetConstructor),
      "constructor",
    )
  let #(tag_acc, st) =
    common.alloc_get_set_accessor(
      st,
      fn_proto,
      IteratorN(rt_js_types.IteratorProtoGetToStringTag),
      IteratorN(rt_js_types.IteratorProtoSetToStringTag),
      "[Symbol.toStringTag]",
    )
  let st = common.add_named_property(st, iterator_proto, "constructor", ctor_acc)
  let st =
    common.add_symbol_property(st, iterator_proto, symbol_to_string_tag, tag_acc)
  // %IteratorHelperPrototype% — §27.1.4.1. proto → %IteratorPrototype%;
  // next/return + @@toStringTag = "Iterator Helper".
  let #(helper_methods, st) =
    common.alloc_methods(st, fn_proto, [
      #("next", IteratorN(rt_js_types.IteratorHelperNext), 0),
      #("return", IteratorN(rt_js_types.IteratorHelperReturn), 0),
    ])
  let #(iterator_helper_proto, st) =
    common.init_namespace(st, iterator_proto, "Iterator Helper", helper_methods)
  // %WrapForValidIteratorPrototype% — §27.1.5.2. proto → %IteratorPrototype%;
  // next/return only (no @@toStringTag).
  let #(wrap_methods, st) =
    common.alloc_methods(st, fn_proto, [
      #("next", IteratorN(rt_js_types.WrapForValidIteratorNext), 0),
      #("return", IteratorN(rt_js_types.WrapForValidIteratorReturn), 0),
    ])
  let #(wrap_for_valid_proto, st) =
    common.alloc_proto(
      st,
      Some(iterator_proto),
      common.named_props(wrap_methods),
    )
  #(
    IteratorProtos(
      iterator_proto:,
      array_iter_proto:,
      string_iter_proto:,
      map_iter_proto:,
      set_iter_proto:,
      async_iterator_proto:,
      async_from_sync_proto:,
      iterator:,
      iterator_helper_proto:,
      wrap_for_valid_proto:,
    ),
    st,
  )
}

fn alloc_proto_with_symbol(
  st: InstanceState,
  parent: Handle,
  sym: rt_js_types.SymbolId,
  fn_h: Handle,
) -> #(Handle, InstanceState) {
  let #(prop, st) = common.builtin_property(st, mk_object(fn_h))
  let #(h, st) =
    rt_js_store.t_cell_new(
      st,
      SObject(
        kind: Ordinary,
        proto: Some(parent),
        props: dict.new(),
        symbol_props: [#(sym, prop)],
        elements: NoElements,
        extensible: True,
      ),
    )
  #(h, rt_js_store.t_pin_root(st, h))
}

/// One iterator-kind prototype: `{next: <native>}` with `[@@toStringTag]`.
/// Port of arc `builtins.gleam:458-468 alloc_iterator_proto` — the concrete
/// per-kind `next` token is wired at init (bodies land in a later DAG unit).
fn alloc_iter_proto(
  st: InstanceState,
  fn_proto: Handle,
  iterator_proto: Handle,
  next: NativeToken,
  tag: String,
) -> #(Handle, InstanceState) {
  let #(methods, st) = common.alloc_methods(st, fn_proto, [#("next", next, 0)])
  let #(h, st) =
    common.alloc_proto(st, Some(iterator_proto), common.named_props(methods))
  let st = common.add_to_string_tag(st, h, tag)
  #(h, st)
}

// ── dispatch (arc exec/promises.gleam:1791-2003 Async-from-Sync) ────────────

/// Which %AsyncFromSyncIteratorPrototype% method invoked the shared body.
type AfsKind {
  AfsNext
  AfsReturn
  AfsThrow
}

/// Route an `IteratorNative` token. `AsyncFromSync*` are the §27.1.4.2
/// wrapping natives; `Unwrap`/`Close` are the continuation closures they mint.
pub fn dispatch(
  st: InstanceState,
  n: IteratorNative,
  this: JsVal,
  args: List(JsVal),
) -> #(JsVal, InstanceState) {
  case n {
    AsyncFromSyncNext -> async_from_sync(st, this, args, AfsNext)
    AsyncFromSyncReturn -> async_from_sync(st, this, args, AfsReturn)
    AsyncFromSyncThrow -> async_from_sync(st, this, args, AfsThrow)
    // §27.1.4.4 onFulfilled: `v => ({value: v, done})`.
    AsyncFromSyncUnwrap(done:) -> {
      let v = first_arg_or_undefined(args)
      let #(h, st) = rt_js_async.alloc_iter_result(st, v, done)
      #(mk_object(h), st)
    }
    // §27.1.4.4 onRejected: close inner then rethrow. `close_and_throw` is the
    // §7.4.11 IteratorClose-with-throw-completion policy (original error wins).
    AsyncFromSyncClose(sync_iter:) -> {
      let err = first_arg_or_undefined(args)
      iter_protocol.close_throw(st, mk_object(sync_iter), err)
    }
    // ── ES2025 §27.1 Iterator constructor + prototype helpers ───────────────
    IteratorConstructor ->
      // As a plain call (NewTarget undefined) — §27.1.1.1 step 1.
      throw_type_error(st, "Abstract class Iterator not directly constructable")
    rt_js_types.IteratorFrom -> from(st, args)
    rt_js_types.IteratorZip -> zip(st, args)
    rt_js_types.IteratorZipKeyed -> zip_keyed(st, args)
    rt_js_types.IteratorConcat -> concat(st, args)
    rt_js_types.IteratorPrototypeMap ->
      lazy_helper(st, this, args, HelperMap, "map")
    rt_js_types.IteratorPrototypeFilter ->
      lazy_helper(st, this, args, HelperFilter, "filter")
    rt_js_types.IteratorPrototypeFlatMap ->
      lazy_helper(
        st,
        this,
        args,
        fn(func) { HelperFlatMap(func:, inner: None) },
        "flatMap",
      )
    rt_js_types.IteratorPrototypeTake ->
      take_or_drop(st, this, args, HelperTake, "take")
    rt_js_types.IteratorPrototypeDrop ->
      take_or_drop(st, this, args, HelperDrop, "drop")
    rt_js_types.IteratorPrototypeToArray -> to_array(st, this)
    rt_js_types.IteratorPrototypeForEach -> for_each(st, this, args)
    rt_js_types.IteratorPrototypeReduce -> reduce(st, this, args)
    rt_js_types.IteratorPrototypeSome ->
      bool_consumer(st, this, args, True, "some")
    rt_js_types.IteratorPrototypeEvery ->
      bool_consumer(st, this, args, False, "every")
    rt_js_types.IteratorPrototypeFind -> find(st, this, args)
    rt_js_types.IteratorHelperNext -> helper_next(st, this)
    rt_js_types.IteratorHelperReturn -> helper_return(st, this)
    rt_js_types.WrapForValidIteratorNext -> wrap_next(st, this)
    rt_js_types.WrapForValidIteratorReturn -> wrap_return(st, this)
    rt_js_types.IteratorProtoGetToStringTag -> #(mk_string("Iterator"), st)
    rt_js_types.IteratorProtoGetConstructor -> #(
      mk_object(rt_state.t_realm(st).iterator.constructor),
      st,
    )
    rt_js_types.IteratorProtoSetToStringTag ->
      ignore_proto_setter(st, this, args, IgnoreSetTag)
    rt_js_types.IteratorProtoSetConstructor ->
      ignore_proto_setter(st, this, args, IgnoreSetCtor)
    rt_js_types.ArrayIteratorNext -> array_iterator_next(st, this)
    rt_js_types.MapIteratorNext -> map_iterator_next(st, this)
    rt_js_types.SetIteratorNext -> set_iterator_next(st, this)
    rt_js_types.StringIteratorNext -> string_iterator_next(st, this)
  }
}

// ── §23.1.5.2.1 %ArrayIteratorPrototype%.next() (arc call.gleam:1776) ───────

/// Advance an ArrayIterator one step: re-read the source's live length, read
/// the element via [[Get]] (may run getters/proxy traps), shape per kind, bump
/// the cursor. `index: -1` latches exhaustion (spec's generator-return state).
fn array_iterator_next(
  st: InstanceState,
  this: JsVal,
) -> #(JsVal, InstanceState) {
  use st, iter_h, target, index, kind <- require_array_iter(st, this)
  case index < 0 {
    True -> iter_done(st)
    False -> {
      let #(len, st) = array_source_length(st, target)
      case index >= len {
        True -> iter_done(set_iter_kind(st, iter_h, ArrayIterator(target:, index: -1, kind:)))
        False -> {
          let #(out, st) = case kind {
            ArrayIterKeys -> #(mk_number(JInt(index)), st)
            _ -> {
              let #(elem, st) =
                rt_js_obj.t_get_prop(
                  st,
                  mk_object(target),
                  StringKey(index_key(index)),
                )
              case kind {
                ArrayIterValues -> #(elem, st)
                ArrayIterEntries -> alloc_pair(st, mk_number(JInt(index)), elem)
                ArrayIterKeys -> #(elem, st)
              }
            }
          }
          // [[Get]] may have run user code: re-read the slot when bumping.
          let st =
            set_iter_kind(st, iter_h, ArrayIterator(target:, index: index + 1, kind:))
          iter_yield(st, out)
        }
      }
    }
  }
}

fn require_array_iter(
  st: InstanceState,
  this: JsVal,
  cont: fn(InstanceState, Handle, Handle, Int, ArrayIterKind) ->
    #(JsVal, InstanceState),
) -> #(JsVal, InstanceState) {
  case classify(this) {
    KHandle(h) ->
      case rt_js_store.t_cell_get(st, h) {
        SObject(kind: ArrayIterator(target:, index:, kind:), ..) ->
          cont(st, h, target, index, kind)
        _ -> iter_incompatible(st, "Array")
      }
    _ -> iter_incompatible(st, "Array")
  }
}

/// Live-length read for an ArrayIterator source. Array/Arguments answer from
/// the slot; TypedArray re-validates its buffer witness (§23.1.5.1 step 6.b.i);
/// everything else takes the spec's `? ToLength(? Get(O, "length"))` path.
fn array_source_length(
  st: InstanceState,
  target: Handle,
) -> #(Int, InstanceState) {
  case rt_js_store.t_cell_get(st, target) {
    SObject(kind: ArrayObj(length:), ..) -> #(length, st)
    SObject(kind: ArgumentsObj(length:, ..), ..) -> #(length, st)
    SObject(kind: TypedArrayObj(buffer:, len:, ..), ..) ->
      case rt_js_store.t_cell_get(st, buffer) {
        SObject(kind: ArrayBufferObj(detached: True, ..), ..) ->
          throw_type_error(
            st,
            "Cannot perform operation on a detached ArrayBuffer",
          )
        _ -> #(len, st)
      }
    _ -> {
      let #(len_v, st) =
        rt_js_obj.t_get_prop(st, mk_object(target), StringKey(Named("length")))
      let #(len, st) = rt_js_val.t_to_length(st, len_v)
      // arc ops/array_iterator.gleam:214 — array-LIKEs (Proxy, borrowed
      // .values.call({length: Infinity})) can report unbounded length; bail
      // rather than spin forever. Real Array/Arguments/TA take branches above.
      case len > limits.max_iteration {
        True -> {
          let #(e, st) = new_range_error(st, iteration_budget_msg)
          rt_js_store.t_throw(st, e)
        }
        False -> #(len, st)
      }
    }
  }
}

const iteration_budget_msg = "Array-like length exceeds the maximum supported iteration"

// ── §24.1.5.2.1 %MapIteratorPrototype%.next() (arc call.gleam:1933) ─────────

fn map_iterator_next(st: InstanceState, this: JsVal) -> #(JsVal, InstanceState) {
  use st, iter_h, target, index, kind <- require_map_iter(st, this)
  case index < 0 {
    True -> iter_done(st)
    False -> {
      let step = case rt_js_store.t_cell_get(st, target) {
        SObject(kind: MapObj(entries:), ..) ->
          ordered_entries.next_from(entries, index)
        _ -> None
      }
      case step {
        None ->
          iter_done(set_iter_kind(st, iter_h, MapIterator(target:, index: -1, kind:)))
        Some(#(next_cursor, mk, v)) -> {
          let #(out, st) = case kind {
            MapIterKeys -> #(map_key_to_js(mk), st)
            MapIterValues -> #(v, st)
            MapIterEntries -> alloc_pair(st, map_key_to_js(mk), v)
          }
          let st =
            set_iter_kind(st, iter_h, MapIterator(target:, index: next_cursor, kind:))
          iter_yield(st, out)
        }
      }
    }
  }
}

fn require_map_iter(
  st: InstanceState,
  this: JsVal,
  cont: fn(InstanceState, Handle, Handle, Int, MapIterKind) ->
    #(JsVal, InstanceState),
) -> #(JsVal, InstanceState) {
  case classify(this) {
    KHandle(h) ->
      case rt_js_store.t_cell_get(st, h) {
        SObject(kind: MapIterator(target:, index:, kind:), ..) ->
          cont(st, h, target, index, kind)
        _ -> iter_incompatible(st, "Map")
      }
    _ -> iter_incompatible(st, "Map")
  }
}

// ── §24.2.5.2.1 %SetIteratorPrototype%.next() (arc call.gleam:1880) ─────────

fn set_iterator_next(st: InstanceState, this: JsVal) -> #(JsVal, InstanceState) {
  use st, iter_h, target, index, kind <- require_set_iter(st, this)
  case index < 0 {
    True -> iter_done(st)
    False -> {
      let step = case rt_js_store.t_cell_get(st, target) {
        SObject(kind: SetObj(entries:), ..) ->
          ordered_entries.next_from(entries, index)
        _ -> None
      }
      case step {
        None ->
          iter_done(set_iter_kind(st, iter_h, SetIterator(target:, index: -1, kind:)))
        Some(#(next_cursor, _mk, v)) -> {
          let #(out, st) = case kind {
            SetIterValues -> #(v, st)
            SetIterEntries -> alloc_pair(st, v, v)
          }
          let st =
            set_iter_kind(st, iter_h, SetIterator(target:, index: next_cursor, kind:))
          iter_yield(st, out)
        }
      }
    }
  }
}

fn require_set_iter(
  st: InstanceState,
  this: JsVal,
  cont: fn(InstanceState, Handle, Handle, Int, SetIterKind) ->
    #(JsVal, InstanceState),
) -> #(JsVal, InstanceState) {
  case classify(this) {
    KHandle(h) ->
      case rt_js_store.t_cell_get(st, h) {
        SObject(kind: SetIterator(target:, index:, kind:), ..) ->
          cont(st, h, target, index, kind)
        _ -> iter_incompatible(st, "Set")
      }
    _ -> iter_incompatible(st, "Set")
  }
}

// ── §22.1.5.1.1 %StringIteratorPrototype%.next() ────────────────────────────

/// D10: codepoint-indexed. `char_at` walks UTF-8 codepoints; the source string
/// is immutable so no explicit exhaustion latch is needed, but we still write
/// one for consistency with the other iterators.
fn string_iterator_next(
  st: InstanceState,
  this: JsVal,
) -> #(JsVal, InstanceState) {
  case classify(this) {
    KHandle(h) ->
      case rt_js_store.t_cell_get(st, h) {
        SObject(kind: StringIterator(source:, index:), ..) ->
          case index < 0 {
            True -> iter_done(st)
            False ->
              case js_string.char_at(source, index) {
                None ->
                  iter_done(set_iter_kind(st, h, StringIterator(source:, index: -1)))
                Some(ch) -> {
                  let st =
                    set_iter_kind(st, h, StringIterator(source:, index: index + 1))
                  iter_yield(st, mk_string(ch))
                }
              }
          }
        _ -> iter_incompatible(st, "String")
      }
    _ -> iter_incompatible(st, "String")
  }
}

// ── shared iterator-next helpers ────────────────────────────────────────────

fn iter_done(st: InstanceState) -> #(JsVal, InstanceState) {
  let #(h, st) = rt_js_async.alloc_iter_result(st, mk_undefined(), True)
  #(mk_object(h), st)
}

fn iter_yield(st: InstanceState, value: JsVal) -> #(JsVal, InstanceState) {
  let #(h, st) = rt_js_async.alloc_iter_result(st, value, False)
  #(mk_object(h), st)
}

fn alloc_pair(st: InstanceState, a: JsVal, b: JsVal) -> #(JsVal, InstanceState) {
  let #(h, st) = realm_ops.alloc_array(st, [a, b])
  #(mk_object(h), st)
}

/// Rewrite the iterator's ObjKind, preserving the rest of the slot. Re-reads
/// the cell so a getter that mutated the iterator object is not clobbered.
fn set_iter_kind(
  st: InstanceState,
  iter_h: Handle,
  kind: ObjKind,
) -> InstanceState {
  rt_js_store.t_cell_update(st, iter_h, fn(slot) {
    case slot {
      SObject(..) as obj -> SObject(..obj, kind:)
      other -> other
    }
  })
}

fn iter_incompatible(st: InstanceState, tag: String) -> a {
  throw_type_error(st, tag <> " Iterator next called on incompatible receiver")
}

/// §27.1.4.2 %AsyncFromSyncIteratorPrototype%.next/return/throw — always
/// returns a promise. Any sync throw during the body REJECTS that promise.
fn async_from_sync(
  st: InstanceState,
  this: JsVal,
  args: List(JsVal),
  kind: AfsKind,
) -> #(JsVal, InstanceState) {
  let #(#(promise_h, resolve_h, reject_h), st) =
    rt_js_async.t_new_promise_capability(st)
  let cap_resolve = mk_object(resolve_h)
  let cap_reject = mk_object(reject_h)
  let #(outcome, st) =
    protected(st, fn(st) {
      do_async_from_sync(st, this, args, kind, cap_resolve, cap_reject)
    })
  let st = case outcome {
    rt_js_call.NormalCompletion(_) -> st
    rt_js_call.ThrowCompletion(e) ->
      rt_js_async.t_promise_reject(st, promise_h, e)
  }
  #(mk_object(promise_h), st)
}

fn do_async_from_sync(
  st: InstanceState,
  this: JsVal,
  args: List(JsVal),
  kind: AfsKind,
  cap_resolve: JsVal,
  cap_reject: JsVal,
) -> #(JsVal, InstanceState) {
  let sync_rec = require_async_from_sync(st, this)
  let sync_iter = mk_object(sync_rec)
  // §27.1.6.2.2/.3: return/throw look up per call. next uses the record's
  // cached [[NextMethod]] — 2core's `AsyncFromSyncIterator` stores only the
  // sync-iterator handle, so `.next` is fetched here (arc caches in the slot).
  let #(method, st) = case kind {
    AfsNext -> rt_js_obj.t_get_prop(st, sync_iter, StringKey(Named("next")))
    AfsReturn -> rt_js_obj.t_get_prop(st, sync_iter, StringKey(Named("return")))
    AfsThrow -> rt_js_obj.t_get_prop(st, sync_iter, StringKey(Named("throw")))
  }
  case kind, rt_js_call.is_callable(st, method) {
    // §27.1.4.2.2 step 8: no `return` → resolve `{value: arg, done: true}`.
    AfsReturn, False -> {
      let arg = first_arg_or_undefined(args)
      let #(ir_h, st) = rt_js_async.alloc_iter_result(st, arg, True)
      let #(_, st) =
        rt_js_call.t_call_checked(st, cap_resolve, mk_undefined(), [
          mk_object(ir_h),
        ])
      #(mk_undefined(), st)
    }
    // §27.1.4.2.3 step 8: no `throw` → close inner + throw TypeError.
    AfsThrow, False -> {
      let st = iter_protocol.iterator_close_normal(st, sync_iter)
      throw_type_error(st, "The iterator does not provide a 'throw' method.")
    }
    _, _ -> {
      let #(result_val, st) =
        rt_js_call.t_call_checked(st, method, sync_iter, args)
      case classify(result_val) {
        KHandle(result_h) -> {
          let close_on_rejection = case kind {
            AfsReturn -> False
            AfsNext | AfsThrow -> True
          }
          afs_continuation(
            st,
            result_h,
            sync_rec,
            close_on_rejection,
            cap_resolve,
            cap_reject,
          )
        }
        _ -> throw_type_error(st, "Iterator result is not an object")
      }
    }
  }
}

/// §27.1.4.4 AsyncFromSyncIteratorContinuation — read `done`/`value`,
/// PromiseResolve the value, PerformPromiseThen with an unwrap-to-`{value,
/// done}` fulfiller (+ close-on-reject rejector when applicable), then forward
/// to the outer capability.
fn afs_continuation(
  st: InstanceState,
  result_h: Handle,
  sync_rec: Handle,
  close_on_rejection: Bool,
  cap_resolve: JsVal,
  cap_reject: JsVal,
) -> #(JsVal, InstanceState) {
  let result = mk_object(result_h)
  let #(done_v, st) = rt_js_obj.t_get_prop(st, result, StringKey(Named("done")))
  let done = rt_js_val.to_boolean(done_v)
  let #(inner, st) = rt_js_obj.t_get_prop(st, result, StringKey(Named("value")))
  let #(on_fulfilled, st) =
    alloc_closure(st, IteratorN(AsyncFromSyncUnwrap(done:)))
  let #(on_rejected, st) = case done || !close_on_rejection {
    True -> #(mk_undefined(), st)
    False ->
      alloc_closure(st, IteratorN(AsyncFromSyncClose(sync_iter: sync_rec)))
  }
  // §27.1.4.4 step 12: PerformPromiseThen(valueWrapper, onFulfilled,
  // onRejected, promiseCapability) — the OUTER capability's resolve/reject
  // are the reaction's child directly (arc promises.gleam:2006-2048).
  let #(inner_p, st) = rt_js_async.promise_resolve_static(st, inner)
  let st =
    b_promise.perform_promise_then_with_cap(
      st,
      inner_p,
      on_fulfilled,
      on_rejected,
      cap_resolve,
      cap_reject,
    )
  #(mk_undefined(), st)
}

// ── local helpers ───────────────────────────────────────────────────────────

@external(erlang, "twocore_rt_js_call_ffi", "t_apply_protected")
fn protected(
  st: InstanceState,
  body: fn(InstanceState) -> #(JsVal, InstanceState),
) -> #(rt_js_call.Completion, InstanceState)

fn alloc_closure(
  st: InstanceState,
  tag: NativeToken,
) -> #(JsVal, InstanceState) {
  let #(h, st) =
    rt_js_call.t_native_new(
      st,
      Some(rt_state.t_realm(st).function.prototype),
      tag,
      "",
      1,
      False,
    )
  #(mk_object(h), st)
}

fn require_async_from_sync(st: InstanceState, this: JsVal) -> Handle {
  case classify(this) {
    KHandle(h) ->
      case rt_js_store.t_cell_get(st, h) {
        SObject(kind: AsyncFromSyncIterator(sync_rec:), ..) -> sync_rec
        _ -> throw_type_error(st, "not an Async-from-Sync Iterator")
      }
    _ -> throw_type_error(st, "not an Async-from-Sync Iterator")
  }
}

fn throw_type_error(st: InstanceState, msg: String) -> a {
  let assert Some(js) = st.js_store
  let #(e, st) = js.ops.new_error(st, TypeErr, msg)
  rt_js_store.t_throw(st, e)
}

fn new_type_error(st: InstanceState, msg: String) -> #(JsVal, InstanceState) {
  let assert Some(js) = st.js_store
  js.ops.new_error(st, TypeErr, msg)
}

fn new_range_error(st: InstanceState, msg: String) -> #(JsVal, InstanceState) {
  let assert Some(js) = st.js_store
  js.ops.new_error(st, RangeErr, msg)
}

// ═══════════════════════════════════════════════════════════════════════════
// ES2025 §27.1 Iterator Helpers — port of arc `builtins/iterator.gleam`.
// arc's `#(State, Result(v,e))` → 2core `#(v, InstanceState)` + `t_throw` (D7).
// Where arc branches on `Error(thrown)`, 2core wraps in `protected_any`.
// ═══════════════════════════════════════════════════════════════════════════

/// Wire-compatible with `rt_js_call.Completion` (same `{normal_completion,_}`
/// / `{throw_completion,_}` Erlang tags) but generic over the normal value —
/// so `iterator_step_value`'s `Option(JsVal)` etc. can pass through the FFI
/// try/catch without a JsVal encoding.
type ProtOut(a) {
  NormalCompletion(a)
  ThrowCompletion(JsVal)
}

@external(erlang, "twocore_rt_js_call_ffi", "t_apply_protected")
fn protected_any(
  st: InstanceState,
  body: fn(InstanceState) -> #(a, InstanceState),
) -> #(ProtOut(a), InstanceState)

/// Per-module [[Construct]] dispatch — `new Iterator()` (abstract; only
/// subclass `super()` succeeds). Every other IteratorNative token is
/// `constructible: False`, so reaching this arm is an engine bug.
pub fn dispatch_construct(
  st: InstanceState,
  n: IteratorNative,
  _args: List(JsVal),
  new_target: JsVal,
) -> #(Handle, InstanceState) {
  case n {
    IteratorConstructor -> {
      // §27.1.1.1: NewTarget is undefined or %Iterator% itself → TypeError.
      let self = mk_object(rt_state.t_realm(st).iterator.constructor)
      case rt_js_val.is_undef(new_target) || same_handle(new_target, self) {
        True ->
          throw_type_error(
            st,
            "Abstract class Iterator not directly constructable",
          )
        False -> {
          // OrdinaryCreateFromConstructor(NewTarget, %Iterator.prototype%).
          let #(proto, st) =
            proto_from_new_target(
              st,
              new_target,
              rt_state.t_realm(st).iterator.prototype,
            )
          rt_js_store.t_cell_new(
            st,
            SObject(
              kind: Ordinary,
              proto: Some(proto),
              props: dict.new(),
              symbol_props: [],
              elements: NoElements,
              extensible: True,
            ),
          )
        }
      }
    }
    _ -> rt_js_val.t_throw_type_error(st, "not a constructor")
  }
}

fn same_handle(a: JsVal, b: JsVal) -> Bool {
  case classify(a), classify(b) {
    KHandle(ha), KHandle(hb) -> ha.id == hb.id
    _, _ -> False
  }
}

fn proto_from_new_target(
  st: InstanceState,
  new_target: JsVal,
  fallback: Handle,
) -> #(Handle, InstanceState) {
  let #(proto, st) =
    rt_js_obj.t_get_prop(st, new_target, StringKey(Named("prototype")))
  case classify(proto) {
    KHandle(h) -> #(h, st)
    _ -> #(fallback, st)
  }
}

// ── §27.1.2.1 Iterator.from ( O ) ───────────────────────────────────────────

fn from(st: InstanceState, args: List(JsVal)) -> #(JsVal, InstanceState) {
  let o = first_arg_or_undefined(args)
  // Step 1: GetIteratorFlattenable(O, iterate-string-primitives).
  let #(rec, st) =
    iter_protocol.get_iterator_flattenable(
      st,
      o,
      IterateStrings,
      "Iterator.from argument",
    )
  // Step 2: ? OrdinaryHasInstance(%Iterator%, iterator).
  let ctor = rt_state.t_realm(st).iterator.constructor
  let #(is_iter, st) = rt_js_ops.t_ordinary_has_instance(st, ctor, rec.iterator)
  case is_iter != 0 {
    True -> #(rec.iterator, st)
    False -> {
      let #(h, st) =
        realm_ops.alloc_wrapper(
          st,
          WrapForValidIteratorObj(record: rec),
          rt_state.t_realm(st).wrap_for_valid_proto,
        )
      #(mk_object(h), st)
    }
  }
}

// ── Lazy producers — Iterator.prototype.{map,filter,flatMap} ────────────────

fn lazy_helper(
  st: InstanceState,
  this: JsVal,
  args: List(JsVal),
  make_kind: fn(JsVal) -> IteratorHelperKind,
  name: String,
) -> #(JsVal, InstanceState) {
  use rec, func, st <- consumer_with_callback(st, this, args, name)
  alloc_helper(st, make_kind(func), rec)
}

// ── Lazy producers — Iterator.prototype.{take,drop} ─────────────────────────

fn take_or_drop(
  st: InstanceState,
  this: JsVal,
  args: List(JsVal),
  make_kind: fn(Int) -> IteratorHelperKind,
  name: String,
) -> #(JsVal, InstanceState) {
  use _h <- require_object_of(
    st,
    this,
    "Iterator.prototype." <> name <> " called on non-object",
  )
  // §27.1.4.10 step 3-6: ToNumber(limit) BEFORE GetIteratorDirect. On any
  // abrupt completion / NaN / negative, close `this` then throw.
  let #(remaining, st) = coerce_limit(st, this, args, name)
  let #(rec, st) = get_iterator_direct_for(st, this, name)
  alloc_helper(st, make_kind(remaining), rec)
}

/// §27.1.4.10/12 step 3-6: ToIntegerOrInfinity(ToNumber(limit)) with NaN /
/// negative → RangeError. On any abrupt completion, close `this` first.
fn coerce_limit(
  st: InstanceState,
  this: JsVal,
  args: List(JsVal),
  name: String,
) -> #(Int, InstanceState) {
  let arg = first_arg_or_undefined(args)
  // ToNumber via t_to_number (runs ToPrimitive for objects; may throw).
  let #(nout, st) =
    protected_any(st, fn(st) { rt_js_val.t_to_number(st, arg) })
  case nout {
    ThrowCompletion(thrown) -> iter_protocol.close_throw(st, this, thrown)
    NormalCompletion(n) ->
      case n {
        JNan -> {
          let #(e, st) = new_range_error(st, name <> " limit is NaN")
          iter_protocol.close_throw(st, this, e)
        }
        JPosInf -> #(limits.max_safe_integer, st)
        JNegInf -> {
          let #(e, st) = new_range_error(st, name <> " limit is negative")
          iter_protocol.close_throw(st, this, e)
        }
        JInt(i) if i < 0 -> {
          let #(e, st) = new_range_error(st, name <> " limit is negative")
          iter_protocol.close_throw(st, this, e)
        }
        JFloat(f) if f <. 0.0 -> {
          let #(e, st) = new_range_error(st, name <> " limit is negative")
          iter_protocol.close_throw(st, this, e)
        }
        JInt(i) -> #(i, st)
        JFloat(f) -> #(rt_js_val.float_to_int(f), st)
      }
  }
}

fn alloc_helper(
  st: InstanceState,
  kind: IteratorHelperKind,
  underlying: IteratorRecord,
) -> #(JsVal, InstanceState) {
  alloc_helper_body(st, ClassicHelper(kind:, underlying:, counter: 0))
}

/// Allocate a fresh %IteratorHelper% at suspended-start.
fn alloc_helper_body(
  st: InstanceState,
  body: HelperBody,
) -> #(JsVal, InstanceState) {
  let #(h, st) =
    realm_ops.alloc_wrapper(
      st,
      IteratorHelperObj(gen_state: GenSuspendedStart, body:),
      rt_state.t_realm(st).iterator_helper_proto,
    )
  #(mk_object(h), st)
}

// ── %IteratorHelperPrototype%.next / .return ────────────────────────────────

const helper_receiver_err = "Iterator Helper method called on incompatible receiver"

const helper_running_err = "Iterator Helper is currently being iterated"

/// §27.1.4.1 %IteratorHelperPrototype%.next: GeneratorResume(this, undefined,
/// "Iterator Helper"). All three flavours share one [[GeneratorState]] machine.
fn helper_next(st: InstanceState, this: JsVal) -> #(JsVal, InstanceState) {
  use ref, gen_state, body <- require_helper(st, this)
  use st <- resume(st, ref, gen_state)
  case body {
    ClassicHelper(kind:, underlying:, counter:) ->
      classic_helper_next(st, ref, kind, underlying, counter)
    ZipHelper(members:, mode:, keys:) -> zip_next(st, ref, members, mode, keys)
    ConcatHelper(remaining:, inner:) -> concat_next(st, ref, remaining, inner)
  }
}

fn require_helper(
  st: InstanceState,
  this: JsVal,
  cont: fn(Handle, GeneratorState, HelperBody) -> #(JsVal, InstanceState),
) -> #(JsVal, InstanceState) {
  case classify(this) {
    KHandle(h) ->
      case rt_js_store.t_cell_get(st, h) {
        SObject(kind: IteratorHelperObj(gen_state:, body:), ..) ->
          cont(h, gen_state, body)
        _ -> throw_type_error(st, helper_receiver_err)
      }
    _ -> throw_type_error(st, helper_receiver_err)
  }
}

/// §27.5.3.3 GeneratorResume — the .next() half of the helper generator's
/// lifecycle. `body` runs marked `Executing`; on normal exit re-suspend
/// (unless `body` latched Completed). A throw re-suspends too so the caught
/// completion the outer try observes finds a stable state.
fn resume(
  st: InstanceState,
  ref: Handle,
  gen_state: GeneratorState,
  body: fn(InstanceState) -> #(JsVal, InstanceState),
) -> #(JsVal, InstanceState) {
  case gen_state {
    GenExecuting -> throw_type_error(st, helper_running_err)
    GenCompleted -> iter_done(st)
    GenSuspendedStart | GenSuspendedYield -> {
      let st = set_gen_state(st, ref, GenExecuting)
      let #(out, st) = protected_any(st, body)
      let st = map_gen_state(st, ref, suspend_if_executing)
      case out {
        NormalCompletion(v) -> #(v, st)
        ThrowCompletion(e) -> rt_js_store.t_throw(st, e)
      }
    }
  }
}

/// §27.5.3.4 GeneratorResumeAbrupt(·, ReturnCompletion(undefined), ·) — the
/// .return() half. Suspended-start completes BEFORE closing (reentrant call
/// sees Completed); suspended-yield resumes marked Executing.
fn resume_abrupt(
  st: InstanceState,
  ref: Handle,
  gen_state: GeneratorState,
  body: fn(InstanceState) -> #(JsVal, InstanceState),
) -> #(JsVal, InstanceState) {
  case gen_state {
    GenExecuting -> throw_type_error(st, helper_running_err)
    GenCompleted -> iter_done(st)
    GenSuspendedStart -> body(set_gen_state(st, ref, GenCompleted))
    GenSuspendedYield -> body(set_gen_state(st, ref, GenExecuting))
  }
}

fn suspend_if_executing(gs: GeneratorState) -> GeneratorState {
  case gs {
    GenExecuting -> GenSuspendedYield
    GenSuspendedStart | GenSuspendedYield | GenCompleted -> gs
  }
}

fn set_gen_state(
  st: InstanceState,
  ref: Handle,
  gs: GeneratorState,
) -> InstanceState {
  map_gen_state(st, ref, fn(_prev) { gs })
}

/// The ONE lifecycle write for every %IteratorHelper%: `gen_state` is a
/// sibling of `body`, so a body write can never clobber lifecycle.
fn map_gen_state(
  st: InstanceState,
  ref: Handle,
  update: fn(GeneratorState) -> GeneratorState,
) -> InstanceState {
  rt_js_store.t_cell_update(st, ref, fn(slot) {
    case slot {
      SObject(kind: IteratorHelperObj(gen_state:, ..) as k, ..) ->
        SObject(
          ..slot,
          kind: IteratorHelperObj(..k, gen_state: update(gen_state)),
        )
      other -> other
    }
  })
}

fn classic_helper_next(
  st: InstanceState,
  ref: Handle,
  kind: IteratorHelperKind,
  underlying: IteratorRecord,
  counter: Int,
) -> #(JsVal, InstanceState) {
  case kind {
    HelperMap(func:) -> step_map(st, ref, underlying, func, counter)
    HelperFilter(func:) -> step_filter(st, ref, underlying, func, counter)
    HelperTake(remaining:) -> step_take(st, ref, underlying, remaining)
    HelperDrop(remaining:) -> step_drop(st, ref, underlying, remaining)
    HelperFlatMap(func:, inner:) ->
      step_flat_map(st, ref, underlying, func, inner, counter)
  }
}

/// §27.1.4.2 %IteratorHelperPrototype%.return.
fn helper_return(st: InstanceState, this: JsVal) -> #(JsVal, InstanceState) {
  use ref, gen_state, body <- require_helper(st, this)
  use st <- resume_abrupt(st, ref, gen_state)
  case body {
    ClassicHelper(kind:, underlying:, counter: _) ->
      classic_helper_return(st, ref, kind, underlying)
    ZipHelper(members:, mode: _, keys: _) -> zip_return(st, ref, members)
    ConcatHelper(remaining: _, inner:) -> concat_return(st, ref, inner)
  }
}

fn classic_helper_return(
  st: InstanceState,
  ref: Handle,
  kind: IteratorHelperKind,
  underlying: IteratorRecord,
) -> #(JsVal, InstanceState) {
  // For flatMap, close the inner iterator first (best-effort), then outer.
  let #(inner_res, st) = case kind {
    HelperFlatMap(inner: Some(inner), func: _) ->
      close_normal_catch(st, inner.iterator)
    HelperFlatMap(inner: None, func: _)
    | HelperMap(func: _)
    | HelperFilter(func: _)
    | HelperTake(remaining: _)
    | HelperDrop(remaining: _) -> #(Ok(Nil), st)
  }
  let #(outer_res, st) = close_normal_catch(st, underlying.iterator)
  let st = mark_done(st, ref)
  case inner_res, outer_res {
    Error(e), _ -> rt_js_store.t_throw(st, e)
    _, Error(e) -> rt_js_store.t_throw(st, e)
    Ok(Nil), Ok(Nil) -> iter_done(st)
  }
}

fn step_map(
  st: InstanceState,
  ref: Handle,
  underlying: IteratorRecord,
  func: JsVal,
  count: Int,
) -> #(JsVal, InstanceState) {
  use step, st <- after_step(st, ref, underlying)
  case step {
    None -> finish(st, ref)
    Some(v) -> {
      let st = write_counter(st, ref, count + 1)
      let idx = mk_number(rt_js_val.num_from_int(count))
      case rt_js_call.t_call(st, func, mk_undefined(), [v, idx]) {
        #(rt_js_call.NormalCompletion(mapped), st) -> iter_yield(st, mapped)
        #(rt_js_call.ThrowCompletion(thrown), st) ->
          close_throw_done(st, ref, underlying, thrown)
      }
    }
  }
}

fn step_filter(
  st: InstanceState,
  ref: Handle,
  underlying: IteratorRecord,
  func: JsVal,
  count: Int,
) -> #(JsVal, InstanceState) {
  use step, st <- after_step(st, ref, underlying)
  case step {
    None -> finish(st, ref)
    Some(v) -> {
      let st = write_counter(st, ref, count + 1)
      let idx = mk_number(rt_js_val.num_from_int(count))
      case rt_js_call.t_call(st, func, mk_undefined(), [v, idx]) {
        #(rt_js_call.ThrowCompletion(thrown), st) ->
          close_throw_done(st, ref, underlying, thrown)
        #(rt_js_call.NormalCompletion(selected), st) ->
          case rt_js_val.to_boolean(selected) {
            True -> iter_yield(st, v)
            False -> step_filter(st, ref, underlying, func, count + 1)
          }
      }
    }
  }
}

fn step_take(
  st: InstanceState,
  ref: Handle,
  underlying: IteratorRecord,
  remaining: Int,
) -> #(JsVal, InstanceState) {
  case remaining <= 0 {
    True -> {
      // §27.1.4.11: remaining = 0 → IteratorClose(iterated, ReturnCompletion).
      let #(close_res, st) = close_normal_catch(st, underlying.iterator)
      finish_after_close(st, ref, close_res)
    }
    False -> {
      use step, st <- after_step(st, ref, underlying)
      case step {
        None -> finish(st, ref)
        Some(v) -> {
          let st = write_kind(st, ref, HelperTake(remaining - 1))
          iter_yield(st, v)
        }
      }
    }
  }
}

fn step_drop(
  st: InstanceState,
  ref: Handle,
  underlying: IteratorRecord,
  remaining: Int,
) -> #(JsVal, InstanceState) {
  use step, st <- after_step(st, ref, underlying)
  case step {
    None -> finish(st, ref)
    Some(v) ->
      case remaining > 0 {
        True -> {
          let st = write_kind(st, ref, HelperDrop(remaining - 1))
          step_drop(st, ref, underlying, remaining - 1)
        }
        False -> iter_yield(st, v)
      }
  }
}

fn step_flat_map(
  st: InstanceState,
  ref: Handle,
  underlying: IteratorRecord,
  func: JsVal,
  inner: Option(IteratorRecord),
  count: Int,
) -> #(JsVal, InstanceState) {
  case inner {
    Some(inner_rec) -> {
      let #(step, st) =
        protected_any(st, fn(st) {
          iter_protocol.iterator_step_value(st, inner_rec)
        })
      case step {
        ThrowCompletion(thrown) ->
          // inner.next() threw → close outer (inner is already broken).
          close_throw_done(st, ref, underlying, thrown)
        NormalCompletion(Some(v)) -> iter_yield(st, v)
        NormalCompletion(None) -> {
          // Inner exhausted — clear and pull from outer.
          let st = write_kind(st, ref, HelperFlatMap(func:, inner: None))
          step_flat_map(st, ref, underlying, func, None, count)
        }
      }
    }
    None -> {
      use step, st <- after_step(st, ref, underlying)
      case step {
        None -> finish(st, ref)
        Some(v) -> {
          let idx = mk_number(rt_js_val.num_from_int(count))
          let st = write_counter(st, ref, count + 1)
          case rt_js_call.t_call(st, func, mk_undefined(), [v, idx]) {
            #(rt_js_call.ThrowCompletion(thrown), st) ->
              close_throw_done(st, ref, underlying, thrown)
            #(rt_js_call.NormalCompletion(mapped), st) -> {
              // GetIteratorFlattenable(mapped, reject-primitives).
              let #(open, st) =
                protected_any(st, fn(st) {
                  iter_protocol.get_iterator_flattenable(
                    st,
                    mapped,
                    RejectPrimitives,
                    "flatMap callback result",
                  )
                })
              case open {
                ThrowCompletion(thrown) ->
                  close_throw_done(st, ref, underlying, thrown)
                NormalCompletion(new_inner) -> {
                  let st =
                    write_kind(
                      st,
                      ref,
                      HelperFlatMap(func:, inner: Some(new_inner)),
                    )
                  step_flat_map(
                    st,
                    ref,
                    underlying,
                    func,
                    Some(new_inner),
                    count + 1,
                  )
                }
              }
            }
          }
        }
      }
    }
  }
}

// ── %WrapForValidIteratorPrototype%.next / .return ──────────────────────────

fn wrap_next(st: InstanceState, this: JsVal) -> #(JsVal, InstanceState) {
  use rec <- require_wrap(st, this)
  rt_js_call.t_call_checked(st, rec.next_method, rec.iterator, [])
}

/// §27.1.5.2.2: no `return` → CreateIterResultObject(undefined, true); else
/// forward the return method's result AS-IS (spec does not require Object here).
fn wrap_return(st: InstanceState, this: JsVal) -> #(JsVal, InstanceState) {
  use rec <- require_wrap(st, this)
  case iter_protocol.call_return(st, rec.iterator) {
    #(Ok(iter_protocol.NoReturnMethod), st) -> iter_done(st)
    #(Ok(iter_protocol.Returned(result)), st) -> #(result, st)
    #(Error(thrown), st) -> rt_js_store.t_throw(st, thrown)
  }
}

fn require_wrap(
  st: InstanceState,
  this: JsVal,
  cont: fn(IteratorRecord) -> #(JsVal, InstanceState),
) -> #(JsVal, InstanceState) {
  let err = "WrapForValidIterator method called on incompatible receiver"
  case classify(this) {
    KHandle(h) ->
      case rt_js_store.t_cell_get(st, h) {
        SObject(kind: WrapForValidIteratorObj(record:), ..) -> cont(record)
        _ -> throw_type_error(st, err)
      }
    _ -> throw_type_error(st, err)
  }
}

// ── Eager consumers — toArray, forEach, reduce, some, every, find ───────────

fn to_array(st: InstanceState, this: JsVal) -> #(JsVal, InstanceState) {
  let #(rec, st) = get_iterator_direct_for(st, this, "toArray")
  let #(values, st) = iter_protocol.iterator_to_list(st, rec)
  let #(h, st) = realm_ops.alloc_array(st, values)
  #(mk_object(h), st)
}

fn for_each(
  st: InstanceState,
  this: JsVal,
  args: List(JsVal),
) -> #(JsVal, InstanceState) {
  use rec, func, st <- consumer_with_callback(st, this, args, "forEach")
  for_each_loop(st, rec, func, 0)
}

fn for_each_loop(
  st: InstanceState,
  rec: IteratorRecord,
  func: JsVal,
  counter: Int,
) -> #(JsVal, InstanceState) {
  case iter_protocol.iterator_step_value(st, rec) {
    #(None, st) -> #(mk_undefined(), st)
    #(Some(v), st) -> {
      let idx = mk_number(rt_js_val.num_from_int(counter))
      case rt_js_call.t_call(st, func, mk_undefined(), [v, idx]) {
        #(rt_js_call.ThrowCompletion(thrown), st) ->
          iter_protocol.close_throw(st, rec.iterator, thrown)
        #(rt_js_call.NormalCompletion(_result), st) ->
          for_each_loop(st, rec, func, counter + 1)
      }
    }
  }
}

fn reduce(
  st: InstanceState,
  this: JsVal,
  args: List(JsVal),
) -> #(JsVal, InstanceState) {
  use rec, func, st <- consumer_with_callback(st, this, args, "reduce")
  case args {
    [_, initial, ..] -> reduce_loop(st, rec, func, initial, 0)
    _ ->
      case iter_protocol.iterator_step_value(st, rec) {
        #(None, st) ->
          throw_type_error(st, "Reduce of empty iterator with no initial value")
        #(Some(seed), st) -> reduce_loop(st, rec, func, seed, 1)
      }
  }
}

fn reduce_loop(
  st: InstanceState,
  rec: IteratorRecord,
  func: JsVal,
  acc: JsVal,
  counter: Int,
) -> #(JsVal, InstanceState) {
  case iter_protocol.iterator_step_value(st, rec) {
    #(None, st) -> #(acc, st)
    #(Some(v), st) -> {
      let idx = mk_number(rt_js_val.num_from_int(counter))
      case rt_js_call.t_call(st, func, mk_undefined(), [acc, v, idx]) {
        #(rt_js_call.ThrowCompletion(thrown), st) ->
          iter_protocol.close_throw(st, rec.iterator, thrown)
        #(rt_js_call.NormalCompletion(new_acc), st) ->
          reduce_loop(st, rec, func, new_acc, counter + 1)
      }
    }
  }
}

/// Shared body for some/every. `match_on` = truthiness value that triggers
/// early exit. some → True (returns true), every → False (returns false).
fn bool_consumer(
  st: InstanceState,
  this: JsVal,
  args: List(JsVal),
  match_on: Bool,
  name: String,
) -> #(JsVal, InstanceState) {
  use rec, func, st <- consumer_with_callback(st, this, args, name)
  let #(matched, st) = predicate_loop(st, rec, func, 0, match_on)
  #(mk_bool(option.is_some(matched) == match_on), st)
}

/// Shared loop for some/every/find: step iterator, call predicate(v, idx),
/// early-exit (closing iterator) when truthiness == match_on.
fn predicate_loop(
  st: InstanceState,
  rec: IteratorRecord,
  func: JsVal,
  counter: Int,
  match_on: Bool,
) -> #(Option(JsVal), InstanceState) {
  case iter_protocol.iterator_step_value(st, rec) {
    #(None, st) -> #(None, st)
    #(Some(v), st) -> {
      let idx = mk_number(rt_js_val.num_from_int(counter))
      case rt_js_call.t_call(st, func, mk_undefined(), [v, idx]) {
        #(rt_js_call.ThrowCompletion(thrown), st) ->
          iter_protocol.close_throw(st, rec.iterator, thrown)
        #(rt_js_call.NormalCompletion(result), st) ->
          case rt_js_val.to_boolean(result) == match_on {
            True -> {
              let st = iter_protocol.iterator_close_normal(st, rec.iterator)
              #(Some(v), st)
            }
            False -> predicate_loop(st, rec, func, counter + 1, match_on)
          }
      }
    }
  }
}

fn find(
  st: InstanceState,
  this: JsVal,
  args: List(JsVal),
) -> #(JsVal, InstanceState) {
  use rec, func, st <- consumer_with_callback(st, this, args, "find")
  let #(matched, st) = predicate_loop(st, rec, func, 0, True)
  #(option.unwrap(matched, mk_undefined()), st)
}

// ── SetterThatIgnoresPrototypeProperties — §27.1.3.2/.13 ────────────────────

type IgnoreSetterKey {
  IgnoreSetCtor
  IgnoreSetTag
}

/// If `this` is %Iterator.prototype% itself → TypeError. If `this` is not an
/// Object → TypeError. Otherwise CreateDataPropertyOrThrow(this, key, val).
fn ignore_proto_setter(
  st: InstanceState,
  this: JsVal,
  args: List(JsVal),
  which: IgnoreSetterKey,
) -> #(JsVal, InstanceState) {
  let proto = rt_state.t_realm(st).iterator.prototype
  case classify(this) {
    KHandle(h) ->
      case h.id == proto.id {
        True ->
          throw_type_error(
            st,
            "Cannot assign to read only property of Iterator.prototype",
          )
        False -> {
          let val = first_arg_or_undefined(args)
          let key = case which {
            IgnoreSetCtor -> StringKey(Named("constructor"))
            IgnoreSetTag -> SymbolKey(symbol_to_string_tag)
          }
          let #(ok, st) =
            rt_js_obj.t_define_own_data(st, h, key, val, True, True, True)
          case ok {
            True -> #(mk_undefined(), st)
            False ->
              throw_type_error(st, "Cannot define property on this receiver")
          }
        }
      }
    _ ->
      throw_type_error(
        st,
        "Cannot set property on non-object Iterator receiver",
      )
  }
}

// ── shared prologue helpers ─────────────────────────────────────────────────

/// §7.4.9 GetIteratorDirect on `this` for `Iterator.prototype.<name>`.
fn get_iterator_direct_for(
  st: InstanceState,
  this: JsVal,
  name: String,
) -> #(IteratorRecord, InstanceState) {
  iter_protocol.get_iterator_direct(
    st,
    this,
    "Iterator.prototype." <> name <> " called on non-object",
  )
}

/// Shared prologue for forEach/reduce/some/every/find/map/filter/flatMap:
/// validate `this` is Object, validate callback (closing `this` on failure
/// WITHOUT reading `.next` — §27.1.4.5 step 3 orders callback BEFORE
/// GetIteratorDirect), then GetIteratorDirect.
fn consumer_with_callback(
  st: InstanceState,
  this: JsVal,
  args: List(JsVal),
  name: String,
  cont: fn(IteratorRecord, JsVal, InstanceState) -> #(JsVal, InstanceState),
) -> #(JsVal, InstanceState) {
  use _h <- require_object_of(
    st,
    this,
    "Iterator.prototype." <> name <> " called on non-object",
  )
  let func = first_arg_or_undefined(args)
  case rt_js_call.is_callable(st, func) {
    False ->
      iter_protocol.close_throw_type(
        st,
        this,
        name <> " argument is not callable",
      )
    True -> {
      let #(rec, st) = get_iterator_direct_for(st, this, name)
      cont(rec, func, st)
    }
  }
}

fn require_object_of(
  st: InstanceState,
  this: JsVal,
  msg: String,
  cont: fn(Handle) -> #(JsVal, InstanceState),
) -> #(JsVal, InstanceState) {
  case classify(this) {
    KHandle(h) -> cont(h)
    _ -> throw_type_error(st, msg)
  }
}

/// Step the underlying iterator. If next() throws, mark the helper done and
/// propagate WITHOUT close (the iterator is already broken).
fn after_step(
  st: InstanceState,
  ref: Handle,
  rec: IteratorRecord,
  cont: fn(Option(JsVal), InstanceState) -> #(JsVal, InstanceState),
) -> #(JsVal, InstanceState) {
  let #(step, st) =
    protected_any(st, fn(st) { iter_protocol.iterator_step_value(st, rec) })
  case step {
    NormalCompletion(v) -> cont(v, st)
    ThrowCompletion(thrown) -> rt_js_store.t_throw(mark_done(st, ref), thrown)
  }
}

fn finish(st: InstanceState, ref: Handle) -> #(JsVal, InstanceState) {
  iter_done(mark_done(st, ref))
}

/// The generator body's IfAbruptCloseIterator: close `underlying` while still
/// Executing (reentrant next/return during close throws), then Completed +
/// rethrow.
fn close_throw_done(
  st: InstanceState,
  ref: Handle,
  underlying: IteratorRecord,
  thrown: JsVal,
) -> a {
  let #(original, st) =
    iter_protocol.close_and_throw(st, underlying.iterator, thrown)
  rt_js_store.t_throw(mark_done(st, ref), original)
}

/// `iterator_close_normal` under a catch — a return/close body needs the
/// close's throw as a Result, not a divergence.
fn close_normal_catch(
  st: InstanceState,
  iter: JsVal,
) -> #(Result(Nil, JsVal), InstanceState) {
  let #(out, st) =
    protected_any(st, fn(st) {
      #(Nil, iter_protocol.iterator_close_normal(st, iter))
    })
  case out {
    NormalCompletion(Nil) -> #(Ok(Nil), st)
    ThrowCompletion(e) -> #(Error(e), st)
  }
}

/// Latch the helper's [[GeneratorState]] to `Completed`.
fn mark_done(st: InstanceState, ref: Handle) -> InstanceState {
  set_gen_state(st, ref, GenCompleted)
}

fn write_counter(st: InstanceState, ref: Handle, counter: Int) -> InstanceState {
  use kind, _counter <- update_helper(st, ref)
  #(kind, counter)
}

fn write_kind(
  st: InstanceState,
  ref: Handle,
  kind: IteratorHelperKind,
) -> InstanceState {
  use _kind, counter <- update_helper(st, ref)
  #(kind, counter)
}

/// The ONE body write for every %IteratorHelper% flavour.
fn map_helper_body(
  st: InstanceState,
  ref: Handle,
  update: fn(HelperBody) -> HelperBody,
) -> InstanceState {
  rt_js_store.t_cell_update(st, ref, fn(slot) {
    case slot {
      SObject(kind: IteratorHelperObj(body:, ..) as helper, ..) ->
        SObject(
          ..slot,
          kind: IteratorHelperObj(..helper, body: update(body)),
        )
      other -> other
    }
  })
}

fn update_helper(
  st: InstanceState,
  ref: Handle,
  update: fn(IteratorHelperKind, Int) -> #(IteratorHelperKind, Int),
) -> InstanceState {
  use body <- map_helper_body(st, ref)
  case body {
    ClassicHelper(kind:, underlying:, counter:) -> {
      let #(kind, counter) = update(kind, counter)
      ClassicHelper(kind:, underlying:, counter:)
    }
    ZipHelper(..) | ConcatHelper(..) -> body
  }
}

/// A helper body's normal-completion tail: latch Completed and yield done or
/// propagate the close's throw.
fn finish_after_close(
  st: InstanceState,
  ref: Handle,
  close_res: Result(Nil, JsVal),
) -> #(JsVal, InstanceState) {
  let st = mark_done(st, ref)
  case close_res {
    Error(e) -> rt_js_store.t_throw(st, e)
    Ok(Nil) -> iter_done(st)
  }
}

// ── Iterator.zip / Iterator.zipKeyed ────────────────────────────────────────

/// The parsed `mode` option. `padding` rides on `OptLongest` — the ONLY mode
/// the spec reads it in.
type ZipModeOption {
  OptShortest
  OptStrict
  OptLongest(padding: JsVal)
}

fn zip_mode(opt: ZipModeOption) -> ZipMode {
  case opt {
    OptShortest -> ZipShortest
    OptStrict -> ZipStrict
    OptLongest(padding: _) -> ZipLongest
  }
}

fn zip(st: InstanceState, args: List(JsVal)) -> #(JsVal, InstanceState) {
  let iterables = first_arg_or_undefined(args)
  use _h <- require_object_of(
    st,
    iterables,
    "Iterator.zip iterables argument is not an object",
  )
  let #(mode, st) = zip_options(st, args, "zip")
  let #(input_rec, st) = iter_protocol.get_iterator_sync(st, iterables)
  let #(iters, st) = zip_collect(st, input_rec, [])
  let #(padding, st) = case mode {
    OptLongest(padding: opt) -> zip_padding_iterated(st, opt, iters)
    OptShortest | OptStrict -> #(unread_padding(iters), st)
  }
  alloc_zip(st, iters, zip_mode(mode), padding, None)
}

fn unread_padding(iters: List(IteratorRecord)) -> List(JsVal) {
  list.map(iters, fn(_iter) { mk_undefined() })
}

fn zip_keyed(st: InstanceState, args: List(JsVal)) -> #(JsVal, InstanceState) {
  let iterables = first_arg_or_undefined(args)
  use iterables_h <- require_object_of(
    st,
    iterables,
    "Iterator.zipKeyed iterables argument is not an object",
  )
  let #(mode, st) = zip_options(st, args, "zipKeyed")
  let #(all_keys, st) = rt_js_obj.t_own_keys(st, iterables_h)
  let #(#(keys, iters), st) =
    zip_keyed_collect(st, iterables, iterables_h, all_keys, [], [])
  let #(padding, st) = case mode {
    OptLongest(padding: opt) -> zip_keyed_padding(st, opt, keys, iters)
    OptShortest | OptStrict -> #(unread_padding(iters), st)
  }
  alloc_zip(st, iters, zip_mode(mode), padding, Some(keys))
}

/// Steps 2-7 shared by zip/zipKeyed: GetOptionsObject + mode + padding.
fn zip_options(
  st: InstanceState,
  args: List(JsVal),
  name: String,
) -> #(ZipModeOption, InstanceState) {
  let options = arg_at(args, 1)
  case classify(options) {
    KUndef -> #(OptShortest, st)
    KHandle(_) -> {
      let #(mode_v, st) =
        rt_js_obj.t_get_prop(st, options, StringKey(Named("mode")))
      case classify(mode_v) {
        KUndef -> #(OptShortest, st)
        KStr("shortest") -> #(OptShortest, st)
        KStr("strict") -> #(OptStrict, st)
        KStr("longest") -> {
          let #(pad, st) =
            rt_js_obj.t_get_prop(st, options, StringKey(Named("padding")))
          case classify(pad) {
            KUndef | KHandle(_) -> #(OptLongest(padding: pad), st)
            _ ->
              throw_type_error(
                st,
                "Iterator." <> name <> " padding is not an object",
              )
          }
        }
        _ ->
          throw_type_error(
            st,
            "Iterator."
              <> name
              <> " mode must be \"shortest\", \"longest\", or \"strict\"",
          )
      }
    }
    _ ->
      throw_type_error(st, "Iterator." <> name <> " options is not an object")
  }
}

/// zip step 12: drain the iterables iterator via GetIteratorFlattenable.
fn zip_collect(
  st: InstanceState,
  input_rec: IteratorRecord,
  acc: List(IteratorRecord),
) -> #(List(IteratorRecord), InstanceState) {
  let #(step, st) =
    protected_any(st, fn(st) {
      iter_protocol.iterator_step_value(st, input_rec)
    })
  case step {
    ThrowCompletion(thrown) ->
      close_all_throw(st, collected_iters(acc), thrown)
    NormalCompletion(None) -> #(list.reverse(acc), st)
    NormalCompletion(Some(v)) -> {
      use rec, st <- or_close_all(st, fn() {
        [input_rec.iterator, ..collected_iters(acc)]
      }, fn(st) {
        iter_protocol.get_iterator_flattenable(
          st,
          v,
          RejectPrimitives,
          "Iterator.zip input",
        )
      })
      zip_collect(st, input_rec, [rec, ..acc])
    }
  }
}

fn collected_iters(acc: List(IteratorRecord)) -> List(JsVal) {
  list.reverse(acc) |> list.map(fn(rec) { rec.iterator })
}

/// zip step 14: "longest" padding by ITERATING the padding object.
fn zip_padding_iterated(
  st: InstanceState,
  padding_option: JsVal,
  iters: List(IteratorRecord),
) -> #(List(JsVal), InstanceState) {
  let iter_count = list.length(iters)
  case classify(padding_option) {
    KUndef -> #(list.repeat(mk_undefined(), iter_count), st)
    _ -> {
      let opened = list.map(iters, fn(rec) { rec.iterator })
      use pad_rec, st <- or_close_all(st, fn() { opened }, fn(st) {
        iter_protocol.get_iterator_sync(st, padding_option)
      })
      zip_padding_loop(st, pad_rec, opened, iter_count, [])
    }
  }
}

fn zip_padding_loop(
  st: InstanceState,
  pad_rec: IteratorRecord,
  opened: List(JsVal),
  remaining: Int,
  acc: List(JsVal),
) -> #(List(JsVal), InstanceState) {
  case remaining <= 0 {
    True -> {
      let #(close_res, st) = close_normal_catch(st, pad_rec.iterator)
      case close_res {
        Error(thrown) -> close_all_throw(st, opened, thrown)
        Ok(Nil) -> #(list.reverse(acc), st)
      }
    }
    False -> {
      let #(step, st) =
        protected_any(st, fn(st) {
          iter_protocol.iterator_step_value(st, pad_rec)
        })
      case step {
        ThrowCompletion(thrown) -> close_all_throw(st, opened, thrown)
        NormalCompletion(None) -> #(
          list.append(list.reverse(acc), list.repeat(mk_undefined(), remaining)),
          st,
        )
        NormalCompletion(Some(v)) ->
          zip_padding_loop(st, pad_rec, opened, remaining - 1, [v, ..acc])
      }
    }
  }
}

/// zipKeyed step 12: filter to enumerable non-undefined-valued own props.
fn zip_keyed_collect(
  st: InstanceState,
  iterables: JsVal,
  iterables_h: Handle,
  keys_left: List(ObjectKey),
  keys_acc: List(ObjectKey),
  iters_acc: List(IteratorRecord),
) -> #(#(List(ObjectKey), List(IteratorRecord)), InstanceState) {
  case keys_left {
    [] -> #(#(list.reverse(keys_acc), list.reverse(iters_acc)), st)
    [key, ..rest] -> {
      let opened = fn() { collected_iters(iters_acc) }
      use desc, st <- or_close_all(st, opened, fn(st) {
        #(rt_js_obj.t_get_own_property(st, iterables_h, key), st)
      })
      let enumerable = case desc {
        Some(prop) -> rt_js_types.prop_enumerable(prop)
        None -> False
      }
      case enumerable {
        False ->
          zip_keyed_collect(
            st,
            iterables,
            iterables_h,
            rest,
            keys_acc,
            iters_acc,
          )
        True -> {
          use v, st <- or_close_all(st, opened, fn(st) {
            rt_js_obj.t_get_prop(st, iterables, key)
          })
          case classify(v) {
            KUndef ->
              zip_keyed_collect(
                st,
                iterables,
                iterables_h,
                rest,
                keys_acc,
                iters_acc,
              )
            _ -> {
              use rec, st <- or_close_all(st, opened, fn(st) {
                iter_protocol.get_iterator_flattenable(
                  st,
                  v,
                  RejectPrimitives,
                  "Iterator.zipKeyed input",
                )
              })
              zip_keyed_collect(
                st,
                iterables,
                iterables_h,
                rest,
                [key, ..keys_acc],
                [rec, ..iters_acc],
              )
            }
          }
        }
      }
    }
  }
}

/// zipKeyed step 14: "longest" padding read per key from padding object.
fn zip_keyed_padding(
  st: InstanceState,
  padding_option: JsVal,
  keys: List(ObjectKey),
  iters: List(IteratorRecord),
) -> #(List(JsVal), InstanceState) {
  case classify(padding_option) {
    KUndef -> #(list.repeat(mk_undefined(), list.length(iters)), st)
    _ -> {
      let opened = list.map(iters, fn(rec) { rec.iterator })
      zip_keyed_padding_loop(st, padding_option, opened, keys, [])
    }
  }
}

fn zip_keyed_padding_loop(
  st: InstanceState,
  padding_option: JsVal,
  opened: List(JsVal),
  keys_left: List(ObjectKey),
  acc: List(JsVal),
) -> #(List(JsVal), InstanceState) {
  case keys_left {
    [] -> #(list.reverse(acc), st)
    [key, ..rest] -> {
      use v, st <- or_close_all(st, fn() { opened }, fn(st) {
        rt_js_obj.t_get_prop(st, padding_option, key)
      })
      zip_keyed_padding_loop(st, padding_option, opened, rest, [v, ..acc])
    }
  }
}

/// Allocate the IteratorZip helper — the ONE place iterators pair with padding.
fn alloc_zip(
  st: InstanceState,
  iters: List(IteratorRecord),
  mode: ZipMode,
  padding: List(JsVal),
  keys: Option(List(ObjectKey)),
) -> #(JsVal, InstanceState) {
  let assert Ok(paired) = list.strict_zip(iters, padding)
    as "Iterator.zip padding must have one entry per iterator"
  let members =
    list.map(paired, fn(pair) {
      let #(record, pad) = pair
      ZipOpen(record:, padding: pad)
    })
  alloc_helper_body(st, ZipHelper(members:, mode:, keys:))
}

// ── IteratorZip stepping ────────────────────────────────────────────────────

fn zip_next(
  st: InstanceState,
  ref: Handle,
  members: List(ZipMember),
  mode: ZipMode,
  keys: Option(List(ObjectKey)),
) -> #(JsVal, InstanceState) {
  case members {
    [] -> finish(st, ref)
    _ -> zip_round(st, ref, mode, keys, [], members, [])
  }
}

fn zip_round(
  st: InstanceState,
  ref: Handle,
  mode: ZipMode,
  keys: Option(List(ObjectKey)),
  prev: List(ZipMember),
  rest: List(ZipMember),
  results: List(JsVal),
) -> #(JsVal, InstanceState) {
  case rest {
    [] -> zip_emit(st, ref, keys, list.reverse(prev), list.reverse(results))
    [member, ..tail] ->
      case member {
        ZipExhausted(padding:) ->
          zip_round(st, ref, mode, keys, [member, ..prev], tail, [
            padding,
            ..results
          ])
        ZipOpen(record:, padding:) -> {
          let #(step, st) =
            protected_any(st, fn(st) {
              iter_protocol.iterator_step_value(st, record)
            })
          case step {
            ThrowCompletion(thrown) ->
              close_all_throw_done(st, ref, open_others(prev, tail), thrown)
            NormalCompletion(Some(v)) ->
              zip_round(st, ref, mode, keys, [member, ..prev], tail, [
                v,
                ..results
              ])
            NormalCompletion(None) ->
              case mode {
                ZipShortest -> {
                  let #(close_res, st) =
                    close_all_normal(st, open_others(prev, tail))
                  finish_after_close(st, ref, close_res)
                }
                ZipStrict ->
                  case prev {
                    [] -> zip_strict_check(st, ref, tail)
                    _ -> zip_strict_throw(st, ref, open_others(prev, tail))
                  }
                ZipLongest ->
                  case open_others(prev, tail) {
                    [] -> finish(st, ref)
                    _ ->
                      zip_round(
                        st,
                        ref,
                        mode,
                        keys,
                        [ZipExhausted(padding:), ..prev],
                        tail,
                        [padding, ..results],
                      )
                  }
              }
          }
        }
      }
  }
}

fn zip_strict_check(
  st: InstanceState,
  ref: Handle,
  rest: List(ZipMember),
) -> #(JsVal, InstanceState) {
  case rest {
    [] -> finish(st, ref)
    [ZipExhausted(padding: _), ..tail] -> zip_strict_check(st, ref, tail)
    [ZipOpen(record:, padding: _), ..tail] -> {
      let #(step, st) =
        protected_any(st, fn(st) {
          iter_protocol.iterator_step_done(st, record)
        })
      case step {
        ThrowCompletion(thrown) ->
          close_all_throw_done(st, ref, open_members(tail), thrown)
        NormalCompletion(True) -> zip_strict_check(st, ref, tail)
        NormalCompletion(False) ->
          zip_strict_throw(st, ref, [record.iterator, ..open_members(tail)])
      }
    }
  }
}

fn zip_strict_throw(
  st: InstanceState,
  ref: Handle,
  open: List(JsVal),
) -> #(JsVal, InstanceState) {
  let #(terr, st) =
    new_type_error(
      st,
      "Iterator.zip strict mode: iterators have different lengths",
    )
  close_all_throw_done(st, ref, open, terr)
}

fn zip_emit(
  st: InstanceState,
  ref: Handle,
  keys: Option(List(ObjectKey)),
  members: List(ZipMember),
  results: List(JsVal),
) -> #(JsVal, InstanceState) {
  let st = zip_write_members(st, ref, members)
  case keys {
    None -> {
      let #(arr, st) = realm_ops.alloc_array(st, results)
      iter_yield(st, mk_object(arr))
    }
    Some(ks) -> {
      let #(obj, st) = alloc_zip_keyed_result(st, ks, results)
      iter_yield(st, mk_object(obj))
    }
  }
}

/// zipKeyed finishResults: OrdinaryObjectCreate(null) + define per column.
fn alloc_zip_keyed_result(
  st: InstanceState,
  keys: List(ObjectKey),
  results: List(JsVal),
) -> #(Handle, InstanceState) {
  let #(h, st) =
    rt_js_store.t_cell_new(
      st,
      SObject(
        kind: Ordinary,
        proto: None,
        props: dict.new(),
        symbol_props: [],
        elements: NoElements,
        extensible: True,
      ),
    )
  let st =
    list.zip(keys, results)
    |> list.fold(st, fn(st, pair) {
      let #(key, v) = pair
      let #(_ok, st) =
        rt_js_obj.t_define_own_data(st, h, key, v, True, True, True)
      st
    })
  #(h, st)
}

fn zip_return(
  st: InstanceState,
  ref: Handle,
  members: List(ZipMember),
) -> #(JsVal, InstanceState) {
  let #(close_res, st) = close_all_normal(st, open_members(members))
  finish_after_close(st, ref, close_res)
}

fn open_others(prev: List(ZipMember), tail: List(ZipMember)) -> List(JsVal) {
  list.append(open_members(list.reverse(prev)), open_members(tail))
}

fn open_members(members: List(ZipMember)) -> List(JsVal) {
  list.filter_map(members, fn(m) {
    case m {
      ZipOpen(record:, ..) -> Ok(record.iterator)
      ZipExhausted(padding: _) -> Error(Nil)
    }
  })
}

/// Unwrap a protected op, or IteratorCloseAll with the thrown error — the
/// plural sibling of `iter_protocol.or_close`. `iters` is a thunk so
/// happy-path per-element loops don't pay to build it.
fn or_close_all(
  st: InstanceState,
  iters: fn() -> List(JsVal),
  body: fn(InstanceState) -> #(a, InstanceState),
  cont: fn(a, InstanceState) -> #(b, InstanceState),
) -> #(b, InstanceState) {
  case protected_any(st, body) {
    #(NormalCompletion(v), st) -> cont(v, st)
    #(ThrowCompletion(thrown), st) -> close_all_throw(st, iters(), thrown)
  }
}

/// IteratorCloseAll with a pending throw: close every iterator in REVERSE
/// list order (errors from .return swallowed), then rethrow the original.
fn close_all_throw(st: InstanceState, iters: List(JsVal), original: JsVal) -> a {
  let st =
    list.fold(list.reverse(iters), st, fn(st, it) {
      let #(_superseded, st) = iter_protocol.call_return(st, it)
      st
    })
  rt_js_store.t_throw(st, original)
}

/// zip helper body's IfAbruptCloseIterators.
fn close_all_throw_done(
  st: InstanceState,
  ref: Handle,
  open: List(JsVal),
  thrown: JsVal,
) -> a {
  let st =
    list.fold(list.reverse(open), st, fn(st, it) {
      let #(_superseded, st) = iter_protocol.call_return(st, it)
      st
    })
  rt_js_store.t_throw(mark_done(st, ref), thrown)
}

/// IteratorCloseAll with a normal/return completion: reverse order; first
/// abrupt result wins, remaining closes swallow their errors.
fn close_all_normal(
  st: InstanceState,
  iters: List(JsVal),
) -> #(Result(Nil, JsVal), InstanceState) {
  list.fold(list.reverse(iters), #(Ok(Nil), st), fn(acc, it) {
    let #(completion, st) = acc
    case completion {
      Ok(Nil) -> close_normal_catch(st, it)
      Error(e) -> {
        let #(_superseded, st) = iter_protocol.call_return(st, it)
        #(Error(e), st)
      }
    }
  })
}

fn zip_write_members(
  st: InstanceState,
  ref: Handle,
  members: List(ZipMember),
) -> InstanceState {
  use body <- map_helper_body(st, ref)
  case body {
    ZipHelper(mode:, keys:, members: _) -> ZipHelper(members:, mode:, keys:)
    ClassicHelper(..) | ConcatHelper(..) -> body
  }
}

// ── Iterator.concat ─────────────────────────────────────────────────────────

fn concat(st: InstanceState, args: List(JsVal)) -> #(JsVal, InstanceState) {
  concat_validate(st, args, [])
}

fn concat_validate(
  st: InstanceState,
  items: List(JsVal),
  acc: List(ConcatItem),
) -> #(JsVal, InstanceState) {
  case items {
    [] ->
      alloc_helper_body(
        st,
        ConcatHelper(remaining: list.reverse(acc), inner: None),
      )
    [item, ..rest] ->
      case classify(item) {
        KHandle(_) -> {
          let #(method, st) =
            rt_js_obj.t_get_prop(st, item, SymbolKey(symbol_iterator))
          case classify(method) {
            KUndef | KNull ->
              throw_type_error(st, "Iterator.concat argument is not iterable")
            _ ->
              case rt_js_call.is_callable(st, method) {
                True ->
                  concat_validate(st, rest, [
                    ConcatItem(open_method: method, iterable: item),
                    ..acc
                  ])
                False ->
                  throw_type_error(
                    st,
                    "Iterator.concat argument [Symbol.iterator] is not callable",
                  )
              }
          }
        }
        _ ->
          throw_type_error(st, "Iterator.concat argument is not an object")
      }
  }
}

fn concat_next(
  st: InstanceState,
  ref: Handle,
  remaining: List(ConcatItem),
  inner: Option(IteratorRecord),
) -> #(JsVal, InstanceState) {
  case inner {
    Some(inner_rec) -> {
      let #(step, st) =
        protected_any(st, fn(st) {
          iter_protocol.iterator_step_value(st, inner_rec)
        })
      case step {
        ThrowCompletion(thrown) ->
          rt_js_store.t_throw(concat_mark_done(st, ref), thrown)
        NormalCompletion(Some(v)) -> iter_yield(st, v)
        NormalCompletion(None) -> {
          let st = concat_write(st, ref, remaining, None)
          concat_open_next(st, ref, remaining)
        }
      }
    }
    None -> concat_open_next(st, ref, remaining)
  }
}

fn concat_open_next(
  st: InstanceState,
  ref: Handle,
  remaining: List(ConcatItem),
) -> #(JsVal, InstanceState) {
  case remaining {
    [] -> iter_done(concat_mark_done(st, ref))
    [ConcatItem(open_method: method, iterable:), ..rest] ->
      case rt_js_call.t_call(st, method, iterable, []) {
        #(rt_js_call.ThrowCompletion(thrown), st) ->
          rt_js_store.t_throw(concat_mark_done(st, ref), thrown)
        #(rt_js_call.NormalCompletion(iter), st) -> {
          let #(open, st) =
            protected_any(st, fn(st) {
              iter_protocol.get_iterator_direct(
                st,
                iter,
                "Result of the Symbol.iterator method is not an object",
              )
            })
          case open {
            ThrowCompletion(thrown) ->
              rt_js_store.t_throw(concat_mark_done(st, ref), thrown)
            NormalCompletion(inner) -> {
              let st = concat_write(st, ref, rest, Some(inner))
              concat_next(st, ref, rest, Some(inner))
            }
          }
        }
      }
  }
}

fn concat_return(
  st: InstanceState,
  ref: Handle,
  inner: Option(IteratorRecord),
) -> #(JsVal, InstanceState) {
  let #(close_res, st) = case inner {
    Some(inner_rec) -> close_normal_catch(st, inner_rec.iterator)
    None -> #(Ok(Nil), st)
  }
  finish_after_close(concat_mark_done(st, ref), ref, close_res)
}

fn concat_mark_done(st: InstanceState, ref: Handle) -> InstanceState {
  let st = mark_done(st, ref)
  use body <- map_helper_body(st, ref)
  case body {
    ConcatHelper(remaining:, inner: _) -> ConcatHelper(remaining:, inner: None)
    ClassicHelper(..) | ZipHelper(..) -> body
  }
}

fn concat_write(
  st: InstanceState,
  ref: Handle,
  remaining: List(ConcatItem),
  inner: Option(IteratorRecord),
) -> InstanceState {
  use body <- map_helper_body(st, ref)
  case body {
    ConcatHelper(..) -> ConcatHelper(remaining:, inner:)
    ClassicHelper(..) | ZipHelper(..) -> body
  }
}
